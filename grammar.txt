<program> ::= <item>*

<item> ::= <attribute>* <func>

<func> ::= func <ident> \( <param_list> \) <type>? <block>
<param_list> ::= <param> | <param> , <param_list> | <epsilon>
<param> ::= <ident> <type>

<type> ::= <primitive>
<primitive> ::= int | bool | \(\)

<stmt> ::= <expr_stmt> | <let>
<expr_stmt> ::= <f_expr> ; | <cf_expr> ;?
<let> ::= let <ident> <type>? = <expr> ;

<expr> ::= <f_expr> | <cf_expr>

<f_expr> ::= <constant> | <unary> | <binary> | <group> | <call> | <return>
<constant> ::= <integer> | <boolean> | \(\)
<unary> ::= <unary_op> <expr>
<unary_op> ::= ! | -
<binary> ::= <expr> <binary_op> <expr>
<binary_op> ::= + | - | && | \|\| | = | == | != | < | <= | > | >=
<group> ::= \( <expr> \)
<call> ::= <expr> \( <arg_list> \)
<arg_list> ::= <expr> | <expr> , <arg_list> | <epsilon>
<return> ::= return <expr>?

<cf_expr> ::= <block> | <if> | <loop>
<block> ::= { <stmt>* <expr>? }
<if> ::= if <expr> <block> <else>?
<else> ::= else <block>
<loop> ::= loop <block>

<ident> ::= (a..z | A..Z | _) (a..z | A..Z | _ | 0..9)*
<integer> ::= (0..9)+
<boolean> ::= true | false

/* TODO */

<type> ::= type <ident> = <struct> ;

<struct> ::= struct { <field_list> }
<field_list> ::= <field> | <field> , <field_list> | <epsilon>
<field> ::= <ident> <type>
